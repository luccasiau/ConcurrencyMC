------------------------------ DRAFTS AND NOTES -------------------------------
-- Peterson's algorithm:
-- Source: https://en.wikipedia.org/wiki/Peterson%27s_algorithm
--
-- Global variables:
-- -- flag: array initialized to {false, false}
-- -- turn: variable initialize to 2
--
-- Peterson lock process
-- Process P_id
-- 1: flag[id] := 1
-- 2: turn := 1-id;
-- 3: if (flag[1-id] == true) & (turn == (1-id)) goto 3
-- 4: /* critical section */
-- 5: flag[id] := 0
-- 6 : goto 1 (start process again)
-------------------------------------------------------------------------------

------------------------------------ MAIN -------------------------------------
MODULE main
VAR
  turn: 0..2;
  flag: array 0..1 of boolean;
  p0: process peterson(0, flag, turn);
  p1: process peterson(1, flag, turn);
ASSIGN
  init(turn) := 2;
  init(flag[0]) := FALSE;
  init(flag[1]) := FALSE; 
-------------------------------------------------------------------------------

------------------------------- ALL CONSTRAINTS -------------------------------
-- Processes must start infinitely often
FAIRNESS p0.running = TRUE;
FAIRNESS p1.running = TRUE;

-- Processes cannot stay in critical section forever
FAIRNESS (p0.line != 4);
FAIRNESS (p1.line != 4);

-- Processes cannot both enter critical section simultaneously
CTLSPEC AG((p0.line != 4) | (p1.line != 4));

-- Processes waiting to enter critical section must eventually do so
CTLSPEC AG((p0.line=3) -> (AF(p0.line=4)));
CTLSPEC AG((p1.line=3) -> (AF(p1.line=4)));
-------------------------------------------------------------------------------

---------------------------------- PROCESS ------------------------------------
MODULE peterson(p_id, flag, turn)
 -- Following the same style as assignment 5b
VAR
  line: 1..6;
ASSIGN
  init(line) := 1;
 
  -- Decide which line to execute next
  next(line) :=
    case
      (line=1) : line+1;
      (line=2) : line+1;
      (line=3) & (flag[1-p_id]) & (turn=(1-p_id)) : 3;
      (line=3) : 4;
      (line=4) : {4, 5};
      (line=5) : {5, 6};
      (line=6) : 1;
    esac; 

  next(flag[0]) :=
    case
      (p_id=0 & line=1) : TRUE;
      (p_id=0 & line=5) : FALSE;
      TRUE : flag[0];  -- not change it
    esac;

  next(flag[1]) :=
    case
      (p_id=1 & line=1) : TRUE;
      (p_id=1 & line=5) : FALSE;
      TRUE : flag[1];  -- not change it
    esac;

  -- Updating turn if it is on line 2
  next(turn) :=
    case
      (line=2) : 1 - p_id;
      TRUE: turn;
    esac;
-------------------------------------------------------------------------------
